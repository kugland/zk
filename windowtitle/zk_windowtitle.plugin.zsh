() {
  emulate -LR zsh
  autoload -Uz add-zsh-hook

  # Ellipsize a path to display it in a limited space.
  __zk_windowtitle__ellipsize() {
    emulate -LR zsh
    setopt local_options extended_glob no_sh_word_split

    local -i maxlen
    local -i elm_maxlen
    zstyle -s :plugin:zk_windowtitle dir_maxlen maxlen || maxlen=40
    zstyle -s :plugin:zk_windowtitle elm_maxlen elm_maxlen || elm_maxlen=20

    (( ${#1} < maxlen )) && { REPLY="$1"; return; }

    local -a split=(${(s:/:)1})
    local -a head=()
    local -a tail=()
    local prefix=''
    [[ $1 == /* ]] && prefix='/'
    local -i next=0
    for i in {1..$#split}; do
      (( ${#split[$i]} > $(( elm_maxlen + 3 )) )) && split[$i]=${split[$i]:0:$elm_maxlen}â€¦
    done
    local elm
    while (( ${#:-$prefix${(j:/:)head}/â€¦/${(j:/:)tail}} < $maxlen && $#split )); do
      (( next == 0 )) && { elm=$split[1]; shift split; } || { elm=$split[-1]; shift -p split; }
      (( ${#:-$prefix${(j:/:)head}/$elm/${(j:/:)tail}} > $maxlen && $#elm > 3 )) && elm='â€¦'
      (( next == 0 )) && head+=( $elm ) || tail=( $elm $tail )
      next=$(( !next ))
      [[ $elm = 'â€¦' ]] && break
    done
    if (( $#split == 1 && ${#${split[1]}} <= 3 )); then
      head+=($split[1])
    elif (( $#split )); then
      head+=('â€¦')
    fi
    REPLY=${prefix}${(j:/:)head}/${(j:/:)tail}
    while [[ $REPLY = *'/â€¦/â€¦/'* ]]; do REPLY=${REPLY//\/â€¦\/â€¦/\/â€¦}; done
    while [[ $REPLY = *' â€¦'* ]]; do REPLY=${REPLY// â€¦/â€¦}; done
  }

  # Replace control characters with their Unicode representations.
  __zk_windowtitle__replace_cntl() {
    REPLY="$1"
    REPLY=${${${${REPLY//$'\x00'/â€}//$'\x01'/â}//$'\x02'/â‚}//$'\x03'/âƒ}
    REPLY=${${${${REPLY//$'\x04'/â„}//$'\x05'/â…}//$'\x06'/â†}//$'\x07'/â‡}
    REPLY=${${${${REPLY//$'\x08'/âˆ}//$'\x09'/â‰}//$'\x0a'/âŠ}//$'\x0b'/â‹}
    REPLY=${${${${REPLY//$'\x0c'/âŒ}//$'\x0d'/â}//$'\x0e'/âŽ}//$'\x0f'/â}
    REPLY=${${${${REPLY//$'\x10'/â}//$'\x11'/â‘}//$'\x12'/â’}//$'\x13'/â“}
    REPLY=${${${${REPLY//$'\x14'/â”}//$'\x15'/â•}//$'\x16'/â–}//$'\x17'/â—}
    REPLY=${${${${REPLY//$'\x18'/â˜}//$'\x19'/â™}//$'\x1a'/âš}//$'\x1b'/â›}
    REPLY=${${${${REPLY//$'\x1c'/âœ}//$'\x1d'/â}//$'\x1e'/âž}//$'\x1f'/âŸ}
    REPLY=${REPLY//$'\x7f'/â¡}
  }

  __zk_windowtitle__print_title() {
    emulate -LR zsh
    setopt extended_glob

    local REPLY cmd cwd format

    __zk_windowtitle__replace_cntl "${history[$HISTCMD]:-$1}"
    cmd="$REPLY"
    __zk_windowtitle__replace_cntl "${(%):-%~}"
    __zk_windowtitle__ellipsize "$REPLY"
    cwd="$REPLY"

    if (( ! ${+TMUX} )); then
      zstyle -s :plugin:zk_windowtitle format format
      [[ -z $format ]] && format='%1(p.ðŸ”“ .)%c â€¢ %d'
    else
      zstyle -s :plugin:zk_windowtitle format_tmux format
      [[ -z $format ]] && format='%c'
    fi

    zformat -f REPLY "$format" c:"$cmd" d:"$cwd" p:$(( UID == 0 ))
    print -n '\e]0;'
    print -nr -- "$REPLY"
    print -n '\e\\'
  }

  __zk_windowtitle__precmd() {
    __zk_windowtitle__print_title zsh
  }

  __zk_windowtitle__preexec() {
    emulate -LR zsh

    local cmd="$2"
    local -i maxlen=0
    zstyle -s :plugin:zk_windowtitle cmd_maxlen maxlen
    (( maxlen == 0 )) && maxlen=32
    if (( $#cmd > maxlen )); then
      maxlen=$(( maxlen - 1 ))
      cmd="${cmd[1,$maxlen]}â€¦"
    fi
    __zk_windowtitle__print_title "$cmd"
  }

  zsh-defer -1 __zk_windowtitle__print_title zsh

  add-zsh-hook precmd __zk_windowtitle__precmd
  add-zsh-hook preexec __zk_windowtitle__preexec
}
